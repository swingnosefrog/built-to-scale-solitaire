import org.jetbrains.kotlin.gradle.dsl.JvmTarget 

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.gradleup.shadow:shadow-gradle-plugin:9.2.2"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "kotlin"

    version = "0.0.0"
    ext {
        appName = "built-to-scale-solitaire"
        
        useLocalPaintbox = false
    }
    
    java {
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://clojars.org/repo/" }
        maven { url "https://jitpack.io/" }
        maven { url "https://repo.eclipse.org/content/groups/releases/" }
    }
    
    dependencies {
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_21
            freeCompilerArgs.add("-Xwhen-guards")
        }
    }
}

project(":core") {
    dependencies {
        if (useLocalPaintbox) {
            implementation "com.github.chrislo27:paintbox-core:${paintbox_version}"
            implementation "com.github.chrislo27:paintbox-ui:${paintbox_version}"
        } else {
            implementation "com.github.chrislo27.paintbox:paintbox-core:${paintbox_version}"
            implementation "com.github.chrislo27.paintbox:paintbox-ui:${paintbox_version}"
        }

        implementation "net.beadsproject:beads:3.2"
        implementation "com.badlogicgames.jlayer:jlayer:1.0.1-gdx"
        implementation "com.eclipsesource.minimal-json:minimal-json:0.9.5"
        final var steamworks4jVersion = "1.10.0"
        implementation "com.code-disaster.steamworks4j:steamworks4j:${steamworks4jVersion}"
        implementation "com.code-disaster.steamworks4j:steamworks4j-gdx:${steamworks4jVersion}"
    }
}

project(":desktop") {
    dependencies {
        implementation project(":core")

        if (useLocalPaintbox) {
            implementation "com.github.chrislo27:paintbox-desktop:${paintbox_version}"
        } else {
            implementation "com.github.chrislo27.paintbox:paintbox-desktop:${paintbox_version}"
        }
    }
}